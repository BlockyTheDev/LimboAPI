buildscript {
    dependencies {
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
    }
}

plugins {
    id 'java'
    id "com.github.spotbugs" version "4.7.1" apply false
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

allprojects {
    apply plugin: "com.github.spotbugs"
    compileJava.options.encoding = 'UTF-8'

    group = 'net.elytrium'
    version = '1.0.0'

    ext {
        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            name = 'velocitypowered-repo'
            url = 'https://repo.velocitypowered.com/snapshots/'
        }

        maven {
            name = 'minecraft-libraries'
            url = 'https://libraries.minecraft.net/'
        }

        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            html {
                enabled = true
                destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }
}
